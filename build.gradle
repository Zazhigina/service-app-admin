import nu.studer.gradle.jooq.JooqEdition

plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'nu.studer.jooq' version "${jooqPluginVersion}"
    id 'org.springdoc.openapi-gradle-plugin' version "${springDocOpenApiGradlePluginVersion}"
}

if (!project.hasProperty('buildProfile')) {
    ext.setProperty("buildProfile", "${defaultBuildProfile}")
}

if (project.file("profile-${buildProfile}.gradle").exists()) {
    if (buildProfile == "${defaultBuildProfile}") apply from: "profile-${buildProfile}.gradle"
}

group 'igc.mirror'

base {
    archivesName = "root"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
//    mavenCentral()
    maven {
        url "${mavenRepository}"
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

configurations {
    compileClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    runtimeClasspath {
        resolutionStrategy.activateDependencyLocking()
    }
    annotationProcessor {
        resolutionStrategy.activateDependencyLocking()
    }
}

dependencyLocking {
    lockMode = LockMode.STRICT
}

dependencies {
    // logging
    implementation "ch.qos.logback.contrib:logback-json-classic:${logbackContribJSONVersion}"
    implementation "ch.qos.logback.contrib:logback-jackson:${logbackContribJacksonVersion}"

    // jdbc
    implementation "org.postgresql:postgresql:${postgresJDBCVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"

    //CORE
    implementation 'org.springframework.data:spring-data-commons'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-config'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    // Actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    // JOOQ
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    jooqGenerator "org.postgresql:postgresql:${postgresJDBCVersion}"

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    // fix vulnerability version com.nimbusds:nimbus-jose-jwt
    // implementation ('org.springframework.boot:spring-boot-starter-oauth2-resource-server') {
    //     exclude group: 'com.nimbusds', module: 'nimbus-jose-jwt'
    // }
    // implementation 'com.nimbusds:nimbus-jose-jwt:9.37.2'

    // swagger and OpenAPI
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocOpenApiStarterWebmvcUiVersion}"

    // flyway
    implementation "org.flywaydb:flyway-core:${flywayVersion}"

    // lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // mapstruct
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBinding}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

    //ldap
    implementation("org.springframework.ldap:spring-ldap-core")
    implementation("org.springframework.security:spring-security-ldap")
    implementation("com.unboundid:unboundid-ldapsdk")

    // POI
    implementation "org.apache.poi:poi:${apachePoiVersion}"
    implementation "org.apache.poi:poi-ooxml:${apachePoiVersion}"
}

test {
    useJUnitPlatform()
}

//sourceSets.main.java.srcDirs += "${jooqSrcDirs}"

jooq {
    version = "${jooqVersion}"
    edition = JooqEdition.OSS

    configurations {
        main {
            generationTool {
                logging = 'WARN' //org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = "${dbConnectionString}"
                    user = "${dbUser}"
                    password = "${dbPassword}"
                }
                generator {
                    name = 'org.jooq.codegen.JavaGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = '.*'
                        excludes = '^(flyway).*'
                        schemata {
                            schema {
                                inputSchema = "${dbSchema}"
                            }
                        }
                    }
                    target {
                        packageName = 'jooqdata'
                        directory = "${jooqSrcDirs}"
                    }
                    generate {
                        newline = '\\r\\n'
                        javaTimeTypes = true
                        records = true
                        relations = true
                        pojos = false
                        daos = false
                        deprecated = false
                        instanceFields = true
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

openApi {
    outputDir.set(file("/docs"))
}

tasks.named('generateJooq').configure {
    // make jOOQ task participate in incremental builds (which is also a prerequisite for participating in build caching)
    allInputsDeclared = true

    // make jOOQ task participate in build caching
    outputs.cacheIf { true }
}

bootRun{
    println "Current profile: " + "${buildProfile}"
    systemProperty "spring.profiles.active", "${buildProfile}"
    systemProperty "javax.net.ssl.trustStore", "${project.rootDir}" + "${trustStore}"
    systemProperty "javax.net.ssl.trustStorePassword", "${trustStorePassword}"
}